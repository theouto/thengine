#version 450
#extension GL_EXT_nonuniform_qualifier : enable
#define NEAR 0.1f
#define FAR 500.f

layout(location = 0) in vec3 fragPosWorld;
layout(location = 1) in vec3 fragNormalWorld;
layout(location = 2) in vec2 fragUv;
layout(location = 3) in mat4 FragPosLightSpace;
layout(location = 7) flat in ivec3 fRIDone;
layout(location = 8) flat in ivec3 fRIDtwo;
layout(location = 9) in vec4 fmodifiers;

layout(location = 0) out vec4 outColor;

layout(set = 1, binding = 0) uniform sampler2D storageSampler[];
layout(set = 2, binding = 1) uniform sampler2D frameBuffers[];

layout(push_constant) uniform Push 
{
  mat4 modelMatrix;
  mat4 normalMatrix;
} push;

struct PointLight
{
  vec4 position;
  vec4 color;
};

struct DirectionalLight
{
  vec3 direction;
  vec4 color;
};

layout(set = 0, binding = 0) uniform GlobalUbo 
{
  mat4 projection;
  mat4 view;
  mat4 invView;
  mat4 viewStat;
  vec3 lightPos;
  int numLights;
  vec4 depthValues;
  vec4 ambientLightColor; //RGB Intensity
  PointLight pointLights[10];
  int width;
  int height;
  float near;
  float far;
  mat4 lightSpaceMatrix[4];
  int frameIndex;
} ubo;

const float M_PI = 3.1415926538;

const vec2 invAtan = vec2(0.1591, 0.3183);

//==============================================================================

vec2 SampleSphericalMap(vec3 v)
{
  vec2 uv = vec2(atan(v.z, v.x), asin(v.y));
  uv *= invAtan;
  uv += 0.5;
  return uv;
}

//==============================================================================

mat3 cotangent_frame( vec3 normal, vec3 worldPos, vec2 texCoord )
{
  vec3 Q1 = dFdx(worldPos);
  vec3 Q2 = dFdy(worldPos);
  vec2 st1 = dFdx(texCoord);
  vec2 st2 = dFdy(texCoord);

  vec3 N = normalize(normal);
  vec3 T = normalize(Q1 * st2.t - Q2 * st1.t);
  vec3 B = -normalize(cross(N, T));

  mat3 TBN = mat3(T, B, N);
  return TBN;
}

//==============================================================================

vec3 BurleyDiffuse(float lightAng, float viewAng, float halfAng, vec2 UVs)
{
  float f90 = 0.5f + 2.f * texture(storageSampler[nonuniformEXT(fRIDone[1])], UVs).r * halfAng * halfAng * fmodifiers[0];
  float Fl = pow((1 - lightAng), 5);
  float Fv = pow((1 - viewAng), 5);

  float t1 = 1 + (f90 - 1) * Fl;
  float t2 = 1 + (f90 - 1) * Fv;

  return texture(storageSampler[nonuniformEXT(fRIDone[0])], UVs).xyz/M_PI * t1 * t2;
}

//==============================================================================

float DistributionGGX(vec3 N, vec3 H, float a)
{
  float a2     = a*a;
  float NdotH  = max(dot(N, H), 0.0);
  float NdotH2 = NdotH*NdotH;
	
  float nom    = a2;
  float denom  = (NdotH2 * (a2 - 1.0) + 1.0);
  denom        = M_PI * denom * denom;
	
  return nom / denom;
}

//==============================================================================

vec3 metallic(vec3 fres, float metal)
{
  vec3 kD = vec3(1.f) - fres;
  kD *= 1.f - metal;

  return kD; 
}

//==============================================================================

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
  return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

//==============================================================================

float rand(vec2 co) {
  return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
}

float calculateRandPCF(float currentDepth, vec2 uv, int image)
{
  float shadow = 0.f;

  int steps = 2;
  vec2 textureSized = textureSize(frameBuffers[nonuniformEXT(image)], 0);
  vec2 texelSize = 1.0/textureSized;
  float bias = 0.0005f;//0.00009f;

  for(int x = -steps; x <= steps; ++x)
  {
    for(int y = -steps; y <= steps; ++y)
    {
      vec2 randomOffset = vec2(rand(uv + vec2(x, y)), rand(uv - vec2(x, y))) * texelSize;
      float depth = texture(frameBuffers[nonuniformEXT(image)], uv + vec2(float(x)/steps, float(y)/steps) * texelSize).r;

      float pcfDepth = texture(frameBuffers[nonuniformEXT(image)], uv + vec2(float(x)/steps, float(y)/steps) * texelSize + randomOffset * abs(currentDepth - bias - abs(depth)) * 200).r; 
      shadow += currentDepth - bias < pcfDepth ? 1.0 : 0.0;
    }
  }
  shadow /= (steps * 2) * (steps * 2);
 
  return shadow;
}

float ShadowCalculation(vec3 lightDir, vec3 normal, vec3 pos, int image)
{
  // perform perspective divide
  vec3 projCoords = FragPosLightSpace[image].xyz / FragPosLightSpace[image].w;
  vec2 uv = projCoords.xy * 0.5 + 0.5;
  float currentDepth = projCoords.z ;

  //float shadow = calculateRandPCF(currentDepth, uv, image);
  float shadow = 1.f;
  return clamp(shadow, 0.f, 1.f);
}

//==============================================================================

vec3 surfaceLightingHelper(vec2 UVs, vec3 surfaceNormal, vec3 viewDirection, vec3 F0, vec3 intensity, vec3 directionToLight)
{
  if (dot(normalize(fragNormalWorld), directionToLight) < 0) return vec3(0.f);
  vec3 halfAngle = normalize(directionToLight + viewDirection);

  vec3 fres = fresnelSchlick(clamp(dot(halfAngle, viewDirection), 0.f, 1.f), F0);

  float specular = DistributionGGX(surfaceNormal, halfAngle, clamp(texture(storageSampler[nonuniformEXT(fRIDone[1])], UVs).x * fmodifiers[1], 0.001f, 1.f));

  vec3 diff = BurleyDiffuse(
    max(dot(directionToLight, surfaceNormal), 0.0),
    max(dot(viewDirection, surfaceNormal), 0.0),
    max(dot(directionToLight, halfAngle), 0.0),
    UVs);

  vec3 numerator = specular * (diff + fres);

  float denominator = max(max(dot(surfaceNormal, viewDirection), 0.0) * max(dot(surfaceNormal, directionToLight), 0.0), 0.08);
  vec3 spec = numerator / denominator;

  vec3 kD = metallic(fres, texture(storageSampler[nonuniformEXT(fRIDtwo[2])], UVs).r * fmodifiers[3]);

  float NdotL = max(dot(surfaceNormal, directionToLight), 0.f);

  return (kD * texture(storageSampler[nonuniformEXT(fRIDone[0])], UVs).rgb / M_PI + spec) * intensity * NdotL;
}

vec3 calculateSunLight(DirectionalLight sun, vec3 surfaceNormal, vec2 UVs, vec3 viewDirection, vec3 F0, vec3 cameraPosWorld, int image)
{
  vec3 directionToLight = sun.direction;
  directionToLight = normalize(-directionToLight);
  float shadow = ShadowCalculation(directionToLight, surfaceNormal, cameraPosWorld, image);

  if (shadow <= 0) return vec3(0.f);

  vec3 intensity = sun.color.xyz * sun.color.w;

  return (shadow) * surfaceLightingHelper(UVs, surfaceNormal, viewDirection, F0, intensity, directionToLight);
}

vec3 calculateLights(vec3 surfaceNormal, vec2 UVs, vec3 viewDirection, vec3 F0)
{
  vec3 Lo = vec3(0.f);

  for(int i = 0; i < ubo.numLights; i++)
  {
    PointLight light = ubo.pointLights[i];
	
    vec3 directionToLight = light.position.xyz - fragPosWorld;
	float attenuation = 1.0/dot(directionToLight, directionToLight); //distance squared
	
    directionToLight = normalize(directionToLight);

    vec3 intensity = light.color.xyz * light.color.w * attenuation;

    Lo += surfaceLightingHelper(UVs, surfaceNormal, viewDirection, F0, intensity, directionToLight);;
  }

  return Lo;
}

float LinearizeDepth(float depth) 
{
  return NEAR * FAR / (FAR + depth * (NEAR - FAR));
}

void main()
{
  //vec2 projCoords = vec2(gl_FragCoord.x/ubo.width, gl_FragCoord.y/ubo.height);
  float currDepth = gl_FragCoord.z;

  //float prePassDepth = texture(frameBuffers[nonuniformEXT(5)], projCoords).r;

  //if (prePassDepth < currDepth) discard;

  vec3 cameraPosWorld = ubo.invView[3].xyz;
  vec3 viewDirection = normalize(cameraPosWorld - fragPosWorld);

  vec3 surfaceNormal = normalize(fragNormalWorld);
  vec2 UVs = fragUv;
  mat3 TBN = cotangent_frame(surfaceNormal, fragPosWorld, UVs);

  if (fRIDone[2] != 0)
  {
    vec3 tangentNormal = texture(storageSampler[nonuniformEXT(fRIDone[2])], UVs).xyz * 2 - 1.f;
    surfaceNormal = normalize(TBN * tangentNormal);
  }

  vec3 diffuseLight = ubo.ambientLightColor.xyz * ubo.ambientLightColor.w;
  vec3 specularLight = vec3(0.0);

  DirectionalLight sun;
  sun.direction = normalize(ubo.lightPos);
  sun.color = vec4(1.f, 1.f, 0.7f, 1.5f);

  vec3 F0 = vec3(0.04);
  float halfView = dot(normalize(viewDirection + surfaceNormal), surfaceNormal); 
  F0 = mix(F0, texture(storageSampler[nonuniformEXT(fRIDone[0])], UVs).rgb, texture(storageSampler[nonuniformEXT(fRIDtwo[2])], UVs).r * fmodifiers[3]);

  vec3 Lo = vec3(0.f);

  int image = -1;
  vec3[] debugColours =
  {
    {0.5, 0, 0},
    {0, 0.5, 0},
    {0, 0, 0.5},
    {0.5, 0.5, 0.5}
  };

  vec4 fragPosViewSpace = ubo.view * vec4(fragPosWorld, 1.f);
  float depth = abs(fragPosViewSpace.z);

  for (int i = 0; i < 4; i++) {if (depth < ubo.depthValues[i]) {image = i; break;}}
  if (image < 0) image = 3;

  Lo += calculateSunLight(sun, surfaceNormal, UVs, viewDirection, F0, cameraPosWorld, image);
  Lo += calculateLights(surfaceNormal, UVs, viewDirection, F0);

  //https://www.youtube.com/watch?v=BFld4EBO2RE great video!
  vec3 lambda = exp(-0.0005f * currDepth * vec3(.5f, 1.f, 4.f));

  vec4 diffuse = texture(storageSampler[nonuniformEXT(fRIDone[0])], UVs) * (vec4(diffuseLight, 0.0) * texture(storageSampler[nonuniformEXT(fRIDtwo[1])], UVs).r * fmodifiers[2] +
                    vec4(0.01f, 0.01f, 0.02f, 0.f));

  diffuse = vec4(lambda, 0.f) * diffuse + vec4((1 - lambda), 0.f) * vec4(0.1f, 0.1f, 0.1f, 0.f);

  //outColor = vec4(vec3(depth), 0.f);
  //outColor = vec4(fragPosWorld, 1.f);
  //outColor = diffuse + vec4(Lo, 0.f) + vec4(debugColours[image]/5.f, 0.f);
  outColor = diffuse + vec4(Lo, 0.f);
  //outColor = vec4(surfaceNormal, 1.f);
}
